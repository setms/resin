plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'maven-publish'
}

group = 'org.setms.resin'

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/setms/resin")
        credentials {
            username = System.getenv("GH_PACKAGE_USERNAME")
            password = System.getenv("GH_PACKAGE_TOKEN")
        }
    }
}

dependencies {
    implementation libs.bundles.maven
    implementation libs.resin
    testImplementation libs.junit
    testImplementation libs.hamcrest
    testImplementation libs.mockito
}

test {
    useJUnitPlatform()
}

def userHomeDir = System.properties['user.home']
def pitestHistoryFilePath = "${userHomeDir}/.pitest/cache/${project.group}.${project.name}.${project.version}_pitest_history.bin"
pitest {
    junit5PluginVersion = '1.2.1'
    mutationThreshold = 90
    historyInputLocation = pitestHistoryFilePath
    historyOutputLocation = pitestHistoryFilePath
    features = ["+auto_threads"]
}
tasks.named('pitest') {
    dependsOn test
}
check.dependsOn('pitest')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'resin-maven-plugin'
            from components.java

            pom {
                packaging = 'maven-plugin'
            }
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/setms/resin")
            credentials {
                username = System.getenv("GH_USERNAME")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
}
