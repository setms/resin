plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.6'
    id 'info.solidsoft.pitest' version '1.15.0'
    id 'maven-publish'
}

group = 'org.setms.resin'

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.snakeyaml
    testImplementation libs.junit
    testImplementation libs.hamcrest
}

test {
    useJUnitPlatform()
}

def userHomeDir = System.properties['user.home']
def pitestHistoryFilePath = "${userHomeDir}/.pitest/cache/${project.group}.${project.name}.${project.version}_pitest_history.bin"
pitest {
    junit5PluginVersion = '1.2.1'
    mutationThreshold = 90
    historyInputLocation = pitestHistoryFilePath
    historyOutputLocation = pitestHistoryFilePath
    features = ["+auto_threads"]
}
tasks.named('pitest') {
    dependsOn test
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/setms/resin")
            credentials {
                username = System.getenv("GH_USERNAME")
                password = System.getenv("GH_TOKEN")
            }
        }
    }
}
